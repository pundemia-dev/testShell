pragma ComponentBehavior: Bound

import QtQuick
import M3Shapes
import qs.components
import qs.components.effects
import qs.components.images
import qs.services
import qs.config

Item {
    id: root

    required property int centerWidth
    readonly property color bgColour: Colours.tPalette.surface_container_highest

    implicitWidth: Math.round(centerWidth * 0.7)
    implicitHeight: {
        shape.height; // Force update when shape height changes
        return shape.pathBounds().height;
    }

    MaterialShape {
        id: shape

        anchors.centerIn: parent
        implicitSize: root.implicitWidth

        shape: MaterialShape.ClamShell
        color: Qt.alpha(root.bgColour, 1)
        opacity: root.bgColour.a
        layer.enabled: true
    }

    StyledIcon {
        anchors.centerIn: parent

        text: "" // tabler user
        color: Colours.palette.on_surface_variant
        font.pointSize: Math.max(1, Math.round(root.centerWidth / 4))
        visible: pfp.status !== Image.Ready
    }

    CachingImage {
        id: pfp

        anchors.fill: shape
        path: `${Paths.home}/.face`

        layer.enabled: true
        layer.effect: Mask {
            maskSource: shape
        }
    }
}
